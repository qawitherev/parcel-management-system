locals {
  prefix     = var.environment == "production" ? "" : "${var.environment}."
  app_domain = "${local.prefix}parcel-management.${var.root_domain}"
  api_domain = "api.${local.prefix}parcel-management.${var.root_domain}"
}

resource "aws_acm_certificate" "this" {
  domain_name               = local.app_domain
  subject_alternative_names = [local.api_domain]
  validation_method         = "DNS"

  lifecycle {
    create_before_destroy = true
  }

  tags = var.tags
}

resource "aws_route53_record" "validation" {
  for_each = {
    for dvo in aws_acm_certificate.this.domain_validation_options : dvo.domain_name => {
      name   = dvo.resource_record_name
      record = dvo.resource_record_value
      type   = dvo.resource_record_type
    }
  }

  allow_overwrite = true
  name            = each.value.name
  records         = [each.value.record]
  type            = each.value.type
  zone_id         = var.zone_id
  ttl             = 60
}

resource "aws_acm_certificate_validation" "this" {
  certificate_arn         = aws_acm_certificate.this.arn
  validation_record_fqdns = [for record in aws_route53_record.validation : record.fqdn]
}
